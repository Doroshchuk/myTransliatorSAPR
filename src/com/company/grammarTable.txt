<програма>::=program idn <список_огол1> begin <список_опер1> end.
<список_огол1>::=<список_огол>
<список_огол>::=<тип> <список_ід1> ¶|<список_огол> <тип> <список_ід1> ¶
<тип>::=int|real|label
<список_ід1>::=<список_ід>
<список_ід>::=@ idn|<список_ід> , @ idn
<список_опер1>::=<список_опер>
<список_опер>::=<опер1> ¶|<список_опер> <опер1> ¶
<опер1>::=<опер>
<опер>::=<осн_опер>|idn : <осн_опер>
<осн_опер>::=<вивід>|<ввід>|<присвоєння>|<цикл>|<ум_перехід>|<опер_безум_переходу>
<опер_безум_переходу>::=goto idn
<присвоєння>::=idn = <E1>
<вивід>::=<вивід> <опер_вивед>|cout <опер_вивед>
<опер_вивед>::=<< idn|<< con
<ввід>::=<ввід> <опер_введ>|cin <опер_введ>
<опер_введ>::=>> idn
<ум_перехід>::=if <відн> then <опер>
<цикл>::=for idn = <E1> to <E1> by <E1> while { <лог_вираз1> } <список_опер1> end
<лог_вираз1>::=<лог_вираз>
<лог_вираз>::=<лог_терм1>|<лог_вираз> or <лог_терм1>
<лог_терм1>::=<лог_терм>
<лог_терм>::=<лог_мн1>|<лог_терм> and <лог_мн1>
<лог_мн1>::=<лог_мн>
<лог_мн>::=<відн>|[ <лог_вираз1> ]|not <лог_мн>
<відн>::=<E1> <зн_відн> <E1>
<зн_відн>::=<|>|==|<=|>=|!=
<E1>::=<E>
<E>::=<T1>|<E> + <T1>|<E> - <T1>
<T1>::=<T>
<T>::=<o>|<T> * <o>|<T> / <o>
<o>::=idn|con|( <E1> )
